buildscript {
  repositories {
    mavenLocal()
    maven { url "https://plugins.gradle.org/m2/"}
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'signing' //使用signing plugin做数字签名
apply from: rootDir.canonicalPath + '/.gradle/publish.gradle'

defaultTasks 'shadowJar'

group = 'com.github.wjw465150'
version = "2.9.6"
ext {
  buildName = "easyfastjson"
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
//flatDir(dirs: file('jarsDerectory'))
}


dependencies {
  compile files("lib/jackson-annotations-2.9.6.jar")
  compile files("lib/jackson-core-2.9.6.jar")
  compile files("lib/jackson-databind-2.9.6.jar")
//或者:  compile fileTree(dir: "lib", includes: ["*.jar"])
}

sourceSets {
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['src']
		}
	}
}

javadoc {
  failOnError=false
  destinationDir = file("${projectDir}/doc-api")
  title = "${project.buildName}"
  includes = ['org/wjw/efjson/*']

//  doLast {
//    clean.execute()
//  }
}

shadowJar {
  baseName = project.buildName
  classifier = null
  version = project.version
  
  relocate 'com.fasterxml.jackson', 'org.wjw.efjson.deps.com.fasterxml.jackson'
  
  exclude 'META-INF/maven/**'
  exclude 'META-INF/LICENSE'
  exclude 'META-INF/NOTICE'
  
  manifest {  //<--add
    attributes "Built-By": "wjw465150@gmail.com"
    attributes "Build-Name": "${project.buildName}"
    attributes "Build-Version": "${project.version}"
  }
  
  javadoc.execute()
  
}

uploadArchives {
  repositories.mavenDeployer {
    //为Pom文件做数字签名  
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) } 
            
    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: sonatypeUsername, password: sonatypePassword)
    }
    
    pom.groupId = "${project.group}"
    pom.artifactId = "${project.buildName}"
    pom.version = "${project.version}"
    pom.project {
      name project.name
      packaging 'jar'
      description 'Encapsulates Jackon JSON library, Fast Speed Easy Used Json Lib For java'
      url 'https://github.com/wjw465150/EasyFastJson'
      
      scm {
        url "https://github.com/wjw465150/EasyFastJson"
        connection "https://github.com/wjw465150/EasyFastJson.git"
        developerConnection "https://github.com/wjw465150/EasyFastJson.git"
      }
      
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'Encapsulates Jackon JSON library, Fast Speed Easy Used Json Lib For java'
        }
      }
    
      developers {
        developer {
          id 'wjw465150'
          name 'wjw465150'
        }
      }
    }
  }
}

//为项目生成**.jar/**-javadoc.jar/**-sources.jar
task javadocJar(type: Jar, dependsOn: [shadowJar]) {
    classifier = 'javadoc'
    from 'doc-api'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//为所有的jar包做数字签名  
signing {  
  sign configurations.archives  
}
